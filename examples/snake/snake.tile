/*
    04.26.2025
    Muhammed Yasinhan Ya≈üar
*/

import "../tileraylib.tile";

screenWidth: int = 400;
screenHeight: int = screenWidth;

func run_game(): void {
    
    prev_dirs: int[] = int[50];
    posx: float[] = float[50];
    posy: float[] = float[50];
    dirs: int[] = int[50];

    snake_count: int = 1;
    snake_step: int = screenWidth / 10;
    snake_size: int = snake_step;

    cherry_posx: float = GetRandomValue(0, 10) * snake_size;
    cherry_posy: float = GetRandomValue(0, 10) * snake_size;

    dirs[0] = 4;
    prev_dirs[0] = 4;
    posx[0] = 0;
    posy[0] = 0;

    move_interval: float = 0.1; // Snake moves every 0.1 seconds
    move_timer: float = 0.0;

    bool game_over = false;
	// Main game loop
    while ((bool)WindowShouldClose() == false && game_over == false) {  // ESC closes the window as well

        // Update game logic based on timer
        move_timer = move_timer + GetFrameTime();

        for (i: int = 0; i < snake_count; i++) {
            dirs[i + 1] = prev_dirs[i];
        }

        if ((bool)IsKeyDown('W') && dirs[0] != 2) {
            dirs[0] = 1; // up
        } else if ((bool)IsKeyDown('S') && dirs[0] != 1) {
            dirs[0] = 2; // down
        } else if ((bool)IsKeyDown('A') && dirs[0] != 4) {
            dirs[0] = 3; // left
        } else if ((bool)IsKeyDown('D') && dirs[0] != 3) {
            dirs[0] = 4; // right
        }

        if (move_timer >= move_interval) {
            move_timer = move_timer - move_interval;

            for (i: int = 0; i < snake_count; i++) {
                if (dirs[i] == 1) {
                    posy[i] = posy[i] - snake_step; // up
                } else if (dirs[i] == 2) {
                    posy[i] = posy[i] + snake_step; // down
                } else if (dirs[i] == 3) {
                    posx[i] = posx[i] - snake_step; // left
                } else if (dirs[i] == 4) {
                    posx[i] = posx[i] + snake_step; // right
                }
                

                if (posx[i] + snake_size / 2 > screenWidth) {
                    posx[i] = 0;
                } else if (posx[i] <= -snake_size) {
                    posx[i] = screenWidth - snake_size;
                }
        
                if (posy[i] + snake_size / 2 > screenHeight) {
                    posy[i] = 0;
                } else if (posy[i] <= -snake_size) {
                    posy[i] = screenHeight - snake_size;
                }

                if (posx[0] == posx[i] && posy[0] == posy[i] && i != 0) {
                    game_over = true;
                }
            }    
        
            for (i: int = 0; i < snake_count; i++) {
                prev_dirs[i] = dirs[i];
            }

            // add a new part to the snake when snake eat the cherry
            bool is_snake_eat_cherry = (int)posx[0] == (int)cherry_posx && (int)posy[0] == (int)cherry_posy;
            if (is_snake_eat_cherry) {
                int j = snake_count - 1;

                if (dirs[j] == 1) {
                    posy[snake_count] = posy[j] + snake_step;
                    posx[snake_count] = posx[j];
                } else if (dirs[j] == 2) {
                    posy[snake_count] = posy[j] - snake_step;
                    posx[snake_count] = posx[j];
                } else if (dirs[j] == 3) {
                    posy[snake_count] = posy[j];
                    posx[snake_count] = posx[j] + snake_step;
                } else if (dirs[j] == 4) {
                    posy[snake_count] = posy[j];
                    posx[snake_count] = posx[j] - snake_step;
                }

                snake_count = snake_count + 1;

                // randomly put the cherry again
                cherry_posx = GetRandomValue(0, 9) * snake_size;
                cherry_posy = GetRandomValue(0, 9) * snake_size;
            }
        }
    
        // Draw
        BeginDrawing();
            ClearBackground(0xFFF5E5F3); // background
            // cherry
            // cherry_color = 0xDE4077FF;
            cherry_color: int = 0xE384A5FF;
            DrawRectangle((int)cherry_posx, (int)cherry_posy, snake_size, snake_size, cherry_color);

            
            snake_color: int = 0xF7A94FFF;
            for (i: int = 0; i < snake_count; i++) {
                DrawRectangle((int)posx[i], (int)posy[i], snake_size, snake_size, snake_color);
            }

            // tiled background
            int line_color = 0x613701FF;
            for (i: int = 0; i < screenWidth;) {
                i = i + snake_size;
                DrawLine(i, 0, i, screenHeight, line_color);
            }
            for (i: int = 0; i < screenHeight;) {
                i = i + snake_size;
                DrawLine(0, i, screenWidth, i, line_color);
            }

        EndDrawing();
    }

}



func main(argc: int): void {

    InitWindow(screenWidth, screenHeight, "Snake Game");
    SetTargetFPS(60);   // Set our game to run at 60 frames-per-second

    SetRandomSeed(4642);

	run_game();

    // De-Initialization
    CloseWindow();
}